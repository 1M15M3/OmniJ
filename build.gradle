apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'application'

sourceCompatibility = 1.7
version = '0.1'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-core:2.3.2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.3.3'
    compile 'commons-cli:commons-cli:1.2'
    compile "org.codehaus.groovy:groovy-all:2.3.3"

    testCompile "com.google:bitcoinj:0.11.3"
    testCompile "org.codehaus.groovy:groovy-all:2.3.3"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}

mainClassName = "com.msgilligan.bitcoin.cli.Status"
def roboExeName = "btcstatus"

jar {
    manifest {
        attributes 'Implementation-Title': 'BTC/MSC Status', 'Implementation-Version': version, 'Consensus-Class': mainClassName
    }
}

task startRegtest(type:Exec) {
  executable 'bitcoind'
  args '-regtest', ' -server'
}

test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true
}

def homePath = System.properties['user.home']
def roboVMHome = "${homePath}/robovm-0.0.14"
def roboLibPath = "robolib"

task copyToRoboLib(type: Copy) {
    into roboLibPath
    from configurations.runtime
}

task roboCompile(type:Exec, dependsOn: [classes, copyToRoboLib]) {
    executable "${roboVMHome}/bin/robovm"
    args '-verbose', '-debug',
            '-cp', "build/classes/main:${roboLibPath}/commons-cli-1.2.jar:${roboLibPath}/jackson-core-2.3.3.jar:${roboLibPath}/jackson-databind-2.3.3.jar:${roboLibPath}/jackson-annotations-2.3.0.jar",
            '-d', 'build',
            '-forcelinkclasses', 'com.android.okhttp.HttpHandler:com.android.org.conscrypt.OpenSSLSocketFactoryImpl',

            '-o', "${roboExeName}",
            "${mainClassName}"
}

task roboRun(type:Exec, dependsOn: roboCompile) {
    executable "build/${roboExeName}"
    args '-rvm:log=debug'
}

// Test Structure
sourceSets {
    integrationTest {
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + sourceSets.main.output + configurations.testRuntime
        java {
            srcDir 'src/integ/java'
        }
        groovy {
            srcDir 'src/integ/groovy'
        }
        resources {
            srcDir 'src/integ/resources'
        }
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    testLogging.showStandardStreams = true
}

task getMasterConsensus(dependsOn: 'classes',  type: JavaExec) {
    main = 'com.msgilligan.mastercoin.consensus.MasterCoreConsensusTool'
    args = [1, 'build/mastercore_consensus.txt']
    classpath = sourceSets.main.runtimeClasspath
}

task getOmniConsensus(dependsOn: 'classes',  type: JavaExec) {
    main = 'com.msgilligan.mastercoin.consensus.OmniwalletConsensusTool'
    args = [1, 'build/omniwallet_consensus.txt']
    classpath = sourceSets.main.runtimeClasspath
}

task getChestConsensus(dependsOn: 'classes',  type: JavaExec) {
    main = 'com.msgilligan.mastercoin.consensus.ChestConsensusTool'
    args = [1, 'build/chest_consensus.txt']
    classpath = sourceSets.main.runtimeClasspath
}

task getConsensus(dependsOn: ['getMasterConsensus', 'getOmniConsensus', 'getChestConsensus']) << {
    println "Consensus files are in build/*_consensus.txt"
}
