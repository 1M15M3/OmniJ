buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:0.4.0'
    }
}

ext.groovyVersion = '2.4.4'
ext.bitcoinjAddonsVersion = '0.0.7-SNAPSHOT'

apply from: 'gradle/groovydoc.gradle'
apply from: 'gradle/asciidoctor.gradle'
apply from: 'gradle/idea.gradle'

allprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'

    sourceCompatibility = 1.7

    group = 'foundation.omni'
    version = '0.3-SNAPSHOT'

    repositories {
        jcenter()
        mavenLocal()   // Uncomment to work with local snapshots
        maven { url 'https://dl.bintray.com/msgilligan/maven' } // bitcoinj-addons (RPC client)
//      maven { url 'https://oss.sonatype.org/content/repositories/snapshots/'}  // bitcoinj snapshots
    }
}

subprojects {

    dependencies {
        compile 'org.bitcoinj:bitcoinj-core:0.13.2'
        compile 'org.slf4j:slf4j-api:1.7.7'

        runtime 'org.slf4j:slf4j-jdk14:1.7.7'               // Runtime implementation of slf4j

        testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
            exclude module: "groovy-all"
        }
    }

    compileJava {
        options.compilerArgs << '-Xlint:unchecked'
    }

    test {
        testLogging.showStandardStreams = true
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

task jenkinsBuild(dependsOn: [build, testReport, groovydocAll, asciidoctor])

