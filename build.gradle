allprojects {
    apply plugin: 'idea'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'

    repositories {
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/'}
    }

    configurations {
        // jansi configuration is a workaround for a broken transitive dependency
        // See: http://forums.gradle.org/gradle/topics/gradle_task_groovydoc_failing_with_noclassdeffounderror
        jansi.extendsFrom(runtime)
    }

    dependencies {
        jansi 'org.fusesource.jansi:jansi:1.11'         // Transitive dependency of GroovyDoc
        jansi 'org.codehaus.groovy:groovy-ant:2.4.1'    // used in Groovy doc

        compile 'org.slf4j:slf4j-api:1.7.6'
        runtime 'org.slf4j:slf4j-jdk14:1.7.5'             // Transitive runtime dependency of bitcoinj

        testRuntime 'org.slf4j:slf4j-jdk14:1.7.5'         // Transitive runtime dependency of bitcoinj

        testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
            exclude module: "groovy-all"
        }
    }

    test {
        // show standard out and standard error of the test JVM(s) on the console
        testLogging.showStandardStreams = true
    }

    groovydoc {
        // Create GroovyDoc for Groovy + Java classes
        source = sourceSets.main.groovy + sourceSets.main.java
        overview = "src/main/java/overview.html"
        groovyClasspath = project.configurations.jansi
    }
}

