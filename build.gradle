buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctorj-diagram:1.5.0'
    }
}

plugins {
    id 'org.asciidoctor.convert'        version '1.5.3'
    id 'com.jfrog.bintray'              version '1.3.1'
}

ext.groovyVersion = '2.4.7'
ext.bitcoinjAddonsVersion = '0.2.1'
ext.slf4jVersion = '1.7.21'

allprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'findbugs'

    version = '0.5.1-SNAPSHOT'
    group = 'foundation.omni'

    repositories {
        jcenter()
        //mavenLocal()   // Uncomment to work with local snapshots
        maven { url 'https://dl.bintray.com/msgilligan/maven' } // bitcoinj-addons (RPC client)
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }

}

subprojects {
    dependencies {
        compile 'org.bitcoinj:bitcoinj-core:0.14.3'
        compile "org.slf4j:slf4j-api:${slf4jVersion}"

        runtime "org.slf4j:slf4j-jdk14:${slf4jVersion}"               // Runtime implementation of slf4j

        testCompile "org.codehaus.groovy:groovy:${groovyVersion}"
        testCompile("org.spockframework:spock-core:1.1-groovy-2.4-rc-2") {
            exclude module: "groovy-all"
        }
    }

    compileJava {
        options.compilerArgs << '-Xlint:unchecked'
    }

    test {
        testLogging.showStandardStreams = true
    }
}

apply from: 'gradle/idea.gradle'
apply from: 'gradle/groovydoc.gradle'
apply from: 'gradle/asciidoctor.gradle'
apply from: 'gradle/bintray.gradle'

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

task jenkinsBuild(dependsOn: [build, testReport, groovydocAll, asciidoctor])

